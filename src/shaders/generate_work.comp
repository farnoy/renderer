#version 450

#extension GL_KHR_shader_subgroup_basic: require
#extension GL_KHR_shader_subgroup_ballot: require

struct VkDrawIndexedIndirectCommand {
    uint indexCount;
    uint instanceCount;
    uint firstIndex;
    uint vertexOffset;
    uint firstInstance;
};

layout(push_constant) uniform PushConstants {
    uint entityIndex;
    uint gltfIndex;
    uint indexCount;
    uint indexOffset; 
    uint vertexOffset; 
};

layout(set = 0, binding = 0) uniform UBO {
    mat4 mvp[1024];
};

layout(set = 1, binding = 0) buffer IndirectCommands {
    VkDrawIndexedIndirectCommand indirect_commands[];
};

layout(set = 1, binding = 1) buffer readonly IndexBuffer {
    uint index_buffer[][3];
};

layout(set = 1, binding = 2) buffer readonly VertexBuffer {
    float vertex_buffer[][3];
};

layout(set = 1, binding = 3) buffer OutIndexBuffer {
    uint out_index_buffer[][3];
};

layout (local_size_x = 64) in;

void main() {
    /*
    if (gl_GlobalInvocationID.x == 0) {
        indirect_commands[gltfIndex].instanceCount = 1;
        indirect_commands[gltfIndex].firstInstance = entityIndex;
        indirect_commands[gltfIndex].firstIndex = indexOffset;
    }
    */

    if (gl_GlobalInvocationID.x >= indexCount / 3) {
        return;
    }

    // TODO: no dynamic indexOffset here, won't work with different meshes
    uint ix0 = index_buffer[gl_GlobalInvocationID.x][0];
    uint ix1 = index_buffer[gl_GlobalInvocationID.x][1];
    uint ix2 = index_buffer[gl_GlobalInvocationID.x][2];
    vec4 input0 = vec4(
        vertex_buffer[vertexOffset + ix0][0],
        vertex_buffer[vertexOffset + ix0][1],
        vertex_buffer[vertexOffset + ix0][2],
        1.0
    );
    vec4 input1 = vec4(
        vertex_buffer[vertexOffset + ix1][0],
        vertex_buffer[vertexOffset + ix1][1],
        vertex_buffer[vertexOffset + ix1][2],
        1.0
    );
    vec4 input2 = vec4(
        vertex_buffer[vertexOffset + ix2][0],
        vertex_buffer[vertexOffset + ix2][1],
        vertex_buffer[vertexOffset + ix2][2],
        1.0
    );
    vec4 vertex0 = mvp[entityIndex] * input0;
    vec4 vertex1 = mvp[entityIndex] * input1;
    vec4 vertex2 = mvp[entityIndex] * input2;

    float det = determinant(mat3(vertex0.xyw, vertex1.xyw, vertex2.xyw));

    bool cond = det > 0;
    uvec4 ballot = subgroupBallot(cond);
    uint count = subgroupBallotBitCount(ballot);

    uint offset = 0;
    if (subgroupElect()) {
        offset = atomicAdd(indirect_commands[gltfIndex].indexCount, count * 3);
        offset += indexOffset; // to resume where previous mesh ended
        offset /= 3;
    }
    
    offset = subgroupBroadcastFirst(offset);

    if (cond) {
        uint local_offset = offset + subgroupBallotExclusiveBitCount(ballot);
        out_index_buffer[local_offset][0] = ix0;
        out_index_buffer[local_offset][1] = ix1;
        out_index_buffer[local_offset][2] = ix2;
    }
}
